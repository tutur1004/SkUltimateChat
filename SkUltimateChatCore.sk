
#	Merci de ne surtout pas changer les options !
#	Les configurations ce trouvent dans le fichier config.yml !
options:
	file: plugins/SkUltimateChat/config.yml
	prefix: SkUltimateChat
	perms: perm.%{SkUltimateChat-permissions-prefix}%
#
on load:
	wait 1 second
	if file existance of "{@file}" is false:
		download file from "https://raw.githubusercontent.com/tutur1004/SkUltimateChat/master/config.yml" to file "{@file}"
	set {_enable} to yml value "configs.core.enable" of file "{@file}"
	if {_enable} is not true:
		send "[CHAT] Fichier de configuration non remplis !" to console
		send "[CHAT] Merci de le remplir ({@file})" to console
		stop trigger
	if "%now%" do not contain ", ":
		send "[CHAT] Format de date incompatible" to console
		stop trigger
	create directory "plugins/Skript/scripts/SkUltimateModules"
	#	Load des valeurs de config globales (core) !
	set {{@prefix}-permissions-prefix} to yml value "configs.core.permission-prefix" of file "{@file}"
	set {{@prefix}-commandes-prefix} to yml value "configs.core.commandes-prefix" of file "{@file}"
	set {{@prefix}-server-name} to yml value "configs.core.server-name" of file "{@file}"
	set {{@prefix}-host} to yml value "configs.core.MySQL.host" of file "{@file}"
	set {{@prefix}-database} to yml value "configs.core.MySQL.database" of file "{@file}"
	set {{@prefix}-username} to yml value "configs.core.MySQL.username" of file "{@file}"
	set {{@prefix}-password} to yml value "configs.core.MySQL.password" of file "{@file}"
	set {{@prefix}-SSL} to yml value "configs.core.MySQL.SSL" of file "{@file}"
	set {{@prefix}-sql} to the database "mysql://%{{@prefix}-host}%/%{{@prefix}-database}%?user=%{{@prefix}-username}%&password=%{{@prefix}-password}%&useSSL=%{{@prefix}-SSL}%"
	execute "CREATE TABLE IF NOT EXISTS `SkUltimateChat-Chat` (`id` int(11) NOT NULL,`types` varchar(20) DEFAULT NULL,`joueur1` varchar(64) DEFAULT NULL,`joueur2` varchar(64) DEFAULT NULL,`joueur3` varchar(64) DEFAULT NULL,`message` varchar(255) DEFAULT NULL,`server` varchar(32) DEFAULT NULL,`date` varchar(20) DEFAULT NULL) ENGINE=InnoDB DEFAULT CHARSET=utf8 COLLATE=utf8_unicode_ci COMMENT='Table du skript SkUltimateChat par tutur1004, merci de ne pas modifier manuellement les tables !';" in {{@prefix}-sql}
	execute "ALTER TABLE `SkUltimateChat-Chat` ADD PRIMARY KEY (`id`);" in {{@prefix}-sql}
	execute "ALTER TABLE `SkUltimateChat-Chat` MODIFY `id` int(11) NOT NULL AUTO_INCREMENT,AUTO_INCREMENT=1;" in {{@prefix}-sql}
	execute "CREATE TABLE IF NOT EXISTS `SkUltimateChat-User` (`id` int(11) NOT NULL,`name` varchar(64) CHARACTER SET utf8 DEFAULT NULL,`nick` varchar(32) CHARACTER SET utf8 NOT NULL DEFAULT 'no-nick',`server-now` varchar(32) CHARACTER SET utf8 NOT NULL DEFAULT 'disctonect') ENGINE=InnoDB DEFAULT CHARSET=utf8 COLLATE=utf8_unicode_ci COMMENT='Table Table du skript SkUltimateChat par tutur1004, merci de ne pas modifier man';" in {{@prefix}-sql}
	execute "ALTER TABLE `SkUltimateChat-User` ADD PRIMARY KEY (`id`);" in {{@prefix}-sql}
	execute "ALTER TABLE `SkUltimateChat-User` MODIFY `id` int(11) NOT NULL AUTO_INCREMENT,AUTO_INCREMENT=1;" in {{@prefix}-sql}
	#	Load des configuration de modules (noeuds)
	set {_modules::*} to yml nodes "configs.modules" of file "{@file}"
	loop {_modules::*}:
		loop yml nodes "configs.modules.%loop-value%" of file "{@file}":
			if "%loop-value-2%" is not "permissions":
				if "%loop-value-2%" is "white-url":
					set {{@prefix}.white-url::*} to yml list "configs.modules.chat.white-url" of file "{@file}"
				else:
					#	Var = {varprefix.module.content}
					set {{@prefix}.%loop-value-1%.%loop-value-2%} to yml value "configs.modules.%loop-value-1%.%loop-value-2%" of file "{@file}"
					if "%loop-value-2%" contains "module-":
						if {{@prefix}.%loop-value-1%.%loop-value-2%} is true:
							#	Ajout du modules dans la liste des modules à charger
							add "%loop-value-1%" to {_modules.ok::*}
					else if "%loop-value-1%" contains "player-prefix":
						set {_plugin-name} to {{@prefix}.%loop-value-1%.%loop-value-2%}
						add "player-prefix" to {_modules.ok::*}
			else:
				set {_perm-content::*} to yml nodes "configs.modules.%loop-value-1%.permissions" of file "{@file}"
				loop {_perm-content::*}:
					#	Var (Perms) = {perm-prefix.module.perm.content}
					set {{@perms}.%loop-value-1%.perm.%loop-value-3%} to yml value "configs.modules.%loop-value-1%.%loop-value-2%.%loop-value-3%" of file "{@file}"
	#	Module comprenant des functions primaires, à charger en 1er
	if {_modules::*} contains "chat":
		set {_temp} to yml value "configs.modules.chat.perm-plugin" of file "{@file}"
		add "%{_temp}%" to {_modules::*}
	set {_URL} to "https://raw.githubusercontent.com/tutur1004/SkUltimateChat/master/SkUltimateModules/"
	#	Téléchargement des modules.
	set {_count} to 0
	loop {_modules.ok::*}:
		if loop-value is "true":
			stop loop
		else if loop-value is not "player-prefix":
			download file from "%{_URL}%module_%loop-value%.sk" to file "plugins/Skript/scripts/SkUltimateModules/module_%loop-value%.sk"
			execute console command "sk reload SkUltimateModules/module_%loop-value%.sk"
		else:
			download file from "%{_URL}%module_prefix_%{_plugin-name}%.sk" to file "plugins/Skript/scripts/SkUltimateModules/module_prefix_%{_plugin-name}%.sk"
			execute console command "sk reload SkUltimateModules/module_prefix_%{_plugin-name}%.sk"
		#	Modules Counts
		add 1 to {_count}
	#	Modules Ok ?
	send "[CHAT] {@prefix} a charge %{_count}% modules." to console
#
on unload:
	delete directory "plugins/Skript/scripts/SkUltimateModules"
#

on join:
	set {_sql} to "SELECT `name` FROM `SkUltimateChat-User` WHERE `name` = '%player%'"
	execute unsafe {_sql} in {{@prefix}-sql} and store the result in {_output::*}
	if "%{_output::name}%" not "%player%":
		set {_sql} to "INSERT INTO `minecraft`.`SkUltimateChat-User` (`name`, `server-now`) VALUES ('%player%', '%{{@prefix}-server-name}%');"
		execute unsafe {_sql} in {{@prefix}-sql}
	else:
		set {_sql} to "UPDATE `minecraft`.`SkUltimateChat-User` SET `server-now` = '%{{@prefix}-server-name}%' WHERE `SkUltimateChat-User`.`name` = '%player%';"
		execute unsafe {_sql} in {{@prefix}-sql}
#
on quit:
	set {_sql} to "UPDATE `minecraft`.`SkUltimateChat-User` SET `server-now` = 'disctonect' WHERE `SkUltimateChat-User`.`name` = '%player%';"
	execute unsafe {_sql} in {{@prefix}-sql}



